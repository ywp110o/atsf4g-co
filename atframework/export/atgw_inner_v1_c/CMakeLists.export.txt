cmake_minimum_required(VERSION 3.7.0)
cmake_policy(SET CMP0054 NEW)

project(atgw_inner_v1_c)

########################################################################
# CMake 模块 (递归包含模块, 带颜色输出模块, 平台检测模块)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake_modules")

include("${CMAKE_CURRENT_LIST_DIR}/cmake_modules/EchoWithColor.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/cmake_modules/FindConfigurePackage.cmake")

#####################################################################
# 导入编译器和编译选项配置
include("${CMAKE_CURRENT_LIST_DIR}/cmake_modules/CompilerOption.cmake")
EchoWithColor(COLOR GREEN "-- Build Type: ${CMAKE_BUILD_TYPE}")

########################################################################
# just like ATBUS_MACRO_DATA_SMALL_SIZE
set(ATFRAME_GATEWAY_MACRO_DATA_SMALL_SIZE 3072 CACHE STRING "small message buffer for atgateway connection(used to reduce memory copy when there are many small messages)")
set(ATBUS_MACRO_MSG_LIMIT 65536 CACHE STRING "message size limit of libatbus")
add_compiler_define(ATBUS_MACRO_MSG_LIMIT=${ATBUS_MACRO_MSG_LIMIT})

# 加密算法库检测
# =========== 3rd_party - openssl/libressl/boringssl/mbedtls ===========
if (CRYPTO_USE_OPENSSL OR CRYPTO_USE_LIBRESSL OR CRYPTO_USE_BORINGSSL)
    find_package(OpenSSL)
    if (OPENSSL_FOUND)
        message(STATUS "Crypto enabled.(openssl found)")
        include_directories(${OPENSSL_INCLUDE_DIR})
    else()
        message(FATAL_ERROR "CRYPTO_USE_OPENSSL,CRYPTO_USE_LIBRESSL,CRYPTO_USE_BORINGSSL is set but openssl not found")
    endif()
elseif (CRYPTO_USE_MBEDTLS)
    find_package(MbedTLS)
    if (MBEDTLS_FOUND)
        message(STATUS "Crypto enabled.(mbedtls found)")
        include_directories(${MbedTLS_INCLUDE_DIRS})
    else()
        message(FATAL_ERROR "CRYPTO_USE_MBEDTLS is set but mbedtls not found")
    endif()
else ()
    # try to find openssl or mbedtls
    find_package(OpenSSL)
    if (OPENSSL_FOUND)
        message(STATUS "Crypto enabled.(openssl found)")
        set(CRYPTO_USE_OPENSSL 1)
        include_directories(${OPENSSL_INCLUDE_DIR})
    else ()
        find_package(MbedTLS)
        if (MBEDTLS_FOUND) 
            message(STATUS "Crypto enabled.(mbedtls found)")
            set(CRYPTO_USE_MBEDTLS 1)
            include_directories(${MbedTLS_INCLUDE_DIRS})
        endif()
    endif()
endif()

if (OPENSSL_FOUND)
    EchoWithColor(COLOR Green "-- Dependency: openssl: ${OPENSSL_CRYPTO_LIBRARY}")
    list(APPEND 3RD_PARTY_CRYPT_LINK_NAME ${OPENSSL_CRYPTO_LIBRARY})
elseif (MBEDTLS_FOUND)
    EchoWithColor(COLOR Green "-- Dependency: mbedtls: ${MbedTLS_CRYPTO_LIBRARIES}")
    list(APPEND 3RD_PARTY_CRYPT_LINK_NAME ${MbedTLS_CRYPTO_LIBRARIES})
else ()
    EchoWithColor(COLOR Yellow "-- Dependency: mbedtls not found")
    message(FATAL_ERROR "must at least have one of openssl,libressl or mbedtls.")
endif ()

if (MINGW)
    EchoWithColor(COLOR GREEN "-- MinGW: custom add lib gdi32")
    list(APPEND 3RD_PARTY_CRYPT_LINK_NAME gdi32)
endif()

configure_file(
    "${CMAKE_CURRENT_LIST_DIR}/src/config/atframe_utils_build_feature.h.in"
    "${CMAKE_CURRENT_LIST_DIR}/src/config/atframe_utils_build_feature.h"
    @ONLY
)

# 目标
include_directories("${CMAKE_CURRENT_LIST_DIR}/include" "${CMAKE_CURRENT_LIST_DIR}/src/protocols" "${CMAKE_CURRENT_LIST_DIR}/src")
file(GLOB_RECURSE SRC_LIST
    include/*.h
    src/*.cpp
    src/*.cc
    src/*.c
    src/*.cxx
    src/*.h
    src/*.hpp
)

# The same as export.macro.cmake
# if(WIN32 AND NOT MINGW AND NOT CYGWIN)
#     set(LIB_NAME "libatgw_inner_v1_c")
# else()
set(LIB_NAME "atgw_inner_v1_c")
# endif()

if (APPLE)
    add_library(${LIB_NAME} STATIC ${SRC_LIST})
else()
    add_library(${LIB_NAME} SHARED ${SRC_LIST})
endif()

target_link_libraries(${LIB_NAME}
    ${3RD_PARTY_CRYPT_LINK_NAME}
    ${COMPILER_OPTION_EXTERN_CXX_LIBS}
)

# 安装内容
install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/include
    DESTINATION .
	FILES_MATCHING REGEX ".+\\.h(pp)?$"
    PATTERN ".svn" EXCLUDE
    PATTERN ".git" EXCLUDE
)

install(TARGETS ${LIB_NAME}
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION lib
        ARCHIVE DESTINATION lib)